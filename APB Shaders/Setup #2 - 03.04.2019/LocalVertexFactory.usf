/*=============================================================================
	LocalVertexFactory.hlsl: Local vertex factory shader code.
	Copyright 1998-2008 Epic Games, Inc. All Rights Reserved.
=============================================================================*/

#include "DecalFactoryCommon.usf"

float4x4 LocalToWorld;
float4x4 PreviousLocalToWorld;

//SVP+ 31/03/2010 - We don't need to pass this in any more - using transpose of LocalToWorld instead which 
//is close enough to the inverse for calculating normals - saving 0.5 ms per frame and makes spikes less frequent
//float3x3 WorldToLocal; 
//SVP-

float4 ShadowCoordinateScaleBias;

// APB(SJT) +
// ADDED SJT: Scale & bias for the AO map.
float4 AOCoordinateScaleBias;
// APB(SJT) -

struct FVertexFactoryInput
{
	float4	Position	: POSITION;
	half3	TangentX	: TANGENT;
	// TangentZ.w contains sign of tangent basis determinant
	half4	TangentZ	: NORMAL;
// APB(SJT): Removing vertex colours. +
/*
	half4	Color		: COLOR1;
*/
// APB(SJT) -

#if NUM_MATERIAL_TEXCOORDS
	float2	TexCoords[NUM_MATERIAL_TEXCOORDS] : TEXCOORD0;
#endif

#if NEEDS_VERTEX_LIGHTMAP
	float4 LightMapA : TEXCOORD5;
	float4 LightMapB : TEXCOORD6;
	float4 LightMapC : TEXCOORD7;

// APB(SJT): Optional night factor. +
	#if DOUBLE_LIGHTMAP
		float3 NightFactor	: TEXCOORD8;
	#endif
// APB(SJT) -

#elif NEEDS_SIMPLE_VERTEX_LIGHTMAP
	float4 LightMapA : TEXCOORD5;

// APB(SJT): Optional night factor. +
	#if DOUBLE_LIGHTMAP
		float3 NightFactor	: TEXCOORD6;
	#endif
// APB(SJT) -

#endif

#if NEEDS_LIGHTMAP_COORDINATE
	float2	LightMapCoordinate : COLOR;
#endif

// APB(SJT): Per-vertex AO and shadow map support. +
#if VERTEX_AOMAP
	half AO : BLENDWEIGHT0;
	half VertexShadowMap : BLENDWEIGHT1;
#endif
// APB(SJT) -
};

struct FPositionOnlyVertexFactoryInput
{
	float4	Position	: POSITION;
};

struct FVertexFactoryInterpolants
{
#if WORLD_COORDS
// APB(SJT) MODIFIED: Pack per-vertex AO into the w coordinate. +
	// First row of the tangent to world matrix
	float4	TangentToWorld0_VertexAO	: COLOR0;
	// Last row of the tangent to world matrix in xyz
	float4	TangentToWorld2	: COLOR1;
// APB(SJT) -
#else
// APB(SJT): Removing vertex colours. +
/*
	float4	Color : COLOR0;
*/
// APB(SJT) -
// APB(SJT) +
	#if VERTEX_AOMAP
		// Per-vertex AO.
		float VertexAO : COLOR1;
	#endif
// APB(SJT) -
#endif

#if NEEDS_LIGHTMAP_COORDINATE
// APB(SJT) +
	float4	LightMapCoordinate					: TEXCOORD0;	// MODIFIED SJT: Made float4 to hold second coord set.

	// ADDED SJT: Ambient occlusion texture coordinates.
	float2	AOCoordinate						: TEXCOORD1;
#if NUM_MATERIAL_TEXCOORDS
	float4	TexCoords[(NUM_MATERIAL_TEXCOORDS+1)/2]	: TEXCOORD2;	// MODIFIED SJT: Made texcoord2 to fit in AO coords.
#endif
// APB(SJT) -
#else
#if NUM_MATERIAL_TEXCOORDS
	float4	TexCoords[(NUM_MATERIAL_TEXCOORDS+1)/2]	: TEXCOORD0;
#endif
#endif

#if !COMPILER_SUPPORTS_EMPTY_STRUCTS && !WORLD_COORDS && !NEEDS_LIGHTMAP_COORDINATE && !NUM_MATERIAL_TEXCOORDS
	float4 Dummy : TEXCOORD0;
#endif
};

FMaterialParameters GetMaterialParameters(FVertexFactoryInterpolants Interpolants)
{
	FMaterialParameters	Result;
#if NUM_MATERIAL_TEXCOORDS
	UNROLL
	for(int CoordinateIndex = 0;CoordinateIndex < NUM_MATERIAL_TEXCOORDS;CoordinateIndex += 2)
	{
		Result.TexCoords[CoordinateIndex] = Interpolants.TexCoords[CoordinateIndex/2].xy;
		if(CoordinateIndex + 1 < NUM_MATERIAL_TEXCOORDS)
		{
			Result.TexCoords[CoordinateIndex + 1] = Interpolants.TexCoords[CoordinateIndex/2].wz;
		}
	}
#endif
// APB(SJT): Removing vertex colours. +
/*
#if WORLD_COORDS
*/
	Result.VertexColor = 1;
/*
#else
	Result.VertexColor = Interpolants.Color;
#endif
*/
// APB(SJT) -
	Result.TangentNormal = 0;
	Result.TangentCameraVector = 0;
	Result.TangentReflectionVector = 0;
	Result.ScreenPosition = 0;
	Result.TangentLightVector = 0;
#if WORLD_COORDS
// APB(SJT) MODIFIED: Take .xyz since vertex AO is packed into the w coord. +
	#if DECAL_FACTORY
		Result.TangentToWorld = CalcInvTangentBasisDecal(TangentBias(Interpolants.TangentToWorld0_VertexAO.xyz));
	#else
		Result.TangentToWorld = CalcTangentToWorld(Interpolants.TangentToWorld0_VertexAO.xyz,Interpolants.TangentToWorld2);
	#endif
// APB(SJT) -
#endif
	return Result;
}

#if NEEDS_LIGHTMAP_COORDINATE
float2 GetLightMapCoordinate(FVertexFactoryInterpolants Interpolants)
{
	// MODIFIED APB(SJT): Return just the first 2 components.
	return Interpolants.LightMapCoordinate.xy;
}

// APB(SJT) +
// ADDED APB(SJT): Get the second light map coords.
float2 GetLightMap2Coordinate(FVertexFactoryInterpolants Interpolants)
{
	return Interpolants.LightMapCoordinate.wz;
}

// ADDED SJT: Get the ambient occlusion map coords.
float2 GetAOCoordinate(FVertexFactoryInterpolants Interpolants)
{
	return Interpolants.AOCoordinate;
}
// APB(SJT) -
#endif

// APB(SJT) +
// ADDED SJT: Get per-vertex ambient occlusion. Dummy implementation.
float GetVertexAO(FVertexFactoryInterpolants Interpolants)
{
#if VERTEX_AOMAP
	#if WORLD_COORDS
		return Interpolants.TangentToWorld0_VertexAO.w;
	#else
		return Interpolants.VertexAO;
	#endif
#else
	return 1;
#endif
}
// APB(SJT) -

#if NEEDS_VERTEX_LIGHTMAP
void VertexFactoryGetVertexLightMap(FVertexFactoryInput Input,out float4 LightMapA,out float4 LightMapB,out float4 LightMapC)
{
	LightMapA = Input.LightMapA;
	LightMapB = Input.LightMapB;
	LightMapC = Input.LightMapC;
}
#elif NEEDS_SIMPLE_VERTEX_LIGHTMAP
void VertexFactoryGetSimpleVertexLightMap(FVertexFactoryInput Input,out float4 LightMapA)
{
	LightMapA = Input.LightMapA;
}
#endif

// APB(SJT) +

#if (NEEDS_VERTEX_LIGHTMAP || NEEDS_SIMPLE_VERTEX_LIGHTMAP) && DOUBLE_LIGHTMAP
	float3 VertexFactoryGetVertexNightFactor(FVertexFactoryInput Input)
	{
		return Input.NightFactor;
	}
#endif

#if VERTEX_AOMAP
	// Directional light shadow map accessor (for lit base pass).
	half VertexFactoryGetVertexShadowMap(FVertexFactoryInput Input)
	{
		return Input.VertexShadowMap;
	}
#endif

// APB(SJT) -

float4 CalcWorldPosition(FVertexFactoryInput Input)
{
	return MulMatrix(LocalToWorld,Input.Position);
}

/**
* Get the 3x3 tangent basis vectors for this vertex factory
* this vertex factory will calculate the binormal on-the-fly
*
* @param Input - vertex input stream structure
* @return 3x3 matrix
*/
float3x3 VertexFactoryGetTangentBasis( FVertexFactoryInput Input )
{
	float3x3 Result=0;	
	float4 TangentZ = TangentBias(Input.TangentZ);

#if DECAL_FACTORY	
	Result = ProjectDecalTangents(float3(TangentZ.x,TangentZ.y,TangentZ.z));
#else	
	// pass-thru the tangent
	Result[0] = TangentBias(Input.TangentX);
	// pass-thru the normal
	Result[2] = float3(TangentZ.x,TangentZ.y,TangentZ.z);
	// derive the binormal by getting the cross product of the normal and tangent
	Result[1] = cross(Result[2], Result[0]) * TangentZ.w;	
#endif

	return Result;
}

float4 VertexFactoryGetWorldPosition(FVertexFactoryInput Input)
{
	return CalcWorldPosition(Input);
}

FVertexFactoryInterpolants VertexFactoryGetInterpolants(FVertexFactoryInput Input)
{
	FVertexFactoryInterpolants Interpolants;
#if NUM_MATERIAL_TEXCOORDS
	// Ensure the unused components of the last packed texture coordinate are initialized.
	Interpolants.TexCoords[(NUM_MATERIAL_TEXCOORDS + 1) / 2 - 1] = 0;

#if MATERIAL_DECAL
	#if DECAL_FACTORY
		// first uv pair are the projected decal coords
		Interpolants.TexCoords[0].xy = ComputeDecalTexCoord(Input.Position);
		// next uv pair stores: z=decal attenutation w=distance of vertex to decal origin
		Interpolants.TexCoords[0].z = 1;
		Interpolants.TexCoords[0].w = CalcDistanceToDecalOrigin(Input.Position);
	#else
		Interpolants.TexCoords[0].xy = Input.TexCoords[0];
		Interpolants.TexCoords[0].z = 1;
		Interpolants.TexCoords[0].w = 0;
	#endif
#else
	UNROLL
	for(int CoordinateIndex = 0;CoordinateIndex < NUM_MATERIAL_TEXCOORDS;CoordinateIndex += 2)
	{
		Interpolants.TexCoords[CoordinateIndex / 2].xy = Input.TexCoords[CoordinateIndex];
		if(CoordinateIndex + 1 < NUM_MATERIAL_TEXCOORDS)
		{
			Interpolants.TexCoords[CoordinateIndex / 2].wz = Input.TexCoords[CoordinateIndex + 1];
		}
	}
#endif
#endif

#if NEEDS_LIGHTMAP_COORDINATE
// APB(SJT) +
	// MODIFIED APB(SJT): Compute second lightmap coord set aswell.
	Interpolants.LightMapCoordinate.xy = Input.LightMapCoordinate * ShadowCoordinateScaleBias.xy + ShadowCoordinateScaleBias.wz;
	//Interpolants.LightMapCoordinate.wz = Input.LightMapCoordinate * LightMap2CoordinateScaleBias.xy + LightMap2CoordinateScaleBias.wz;
	Interpolants.LightMapCoordinate.wz = float2(0,0);

	// ADDED SJT: Compute AO coords.
	Interpolants.AOCoordinate = Input.LightMapCoordinate * AOCoordinateScaleBias.xy + AOCoordinateScaleBias.wz;
// APB(SJT) -
#endif

#if WORLD_COORDS
	#if DECAL_FACTORY
		// pass 2x2 matrix for transforming normals as interpolant
		float3 TangentXBiased = TangentBias(Input.TangentX);
		float4 TangentZBiased = TangentBias(Input.TangentZ);
		Interpolants.TangentToWorld2 = CalcDecal2x2NormalTransform(TangentXBiased, TangentZBiased.xyz, TangentZBiased.w) * 0.5 + 0.5;
		// pass through the vertex normal (note the misnomer)
		Interpolants.TangentToWorld0_VertexAO.xyz = float3(TangentZBiased.xyz) * 0.5 + 0.5;
	#else
		float3x3 LocalToTangent = VertexFactoryGetTangentBasis(Input);
		float3x3 TangentToLocal = transpose(LocalToTangent);
		float3x3 TangentToWorld = MulMatrix(LocalToWorld, TangentToLocal);

// APB(SJT) +
		// MODIFIED SJT: Take .xyz since vertex AO is packed into the w coord. +
		Interpolants.TangentToWorld0_VertexAO.xyz = TangentToWorld[0];
		Interpolants.TangentToWorld2 = float4(TangentToWorld[2], 1.0f);
	#endif

	#if VERTEX_AOMAP
		// Set per-vertex AO.
		Interpolants.TangentToWorld0_VertexAO.w = Input.AO;
	#else
		Interpolants.TangentToWorld0_VertexAO.w = 1;
	#endif
// APB(SJT) -
#else
// APB(SJT): Removing vertex colours. +
/*
	Interpolants.Color = Input.Color FCOLOR_COMPONENT_SWIZZLE;
*/
// APB(SJT) -
// APB(SJT) +
	#if VERTEX_AOMAP
		// Set per-vertex AO.
		Interpolants.VertexAO = Input.AO;
	#endif
// APB(SJT) -
#endif

	#if !COMPILER_SUPPORTS_EMPTY_STRUCTS && !WORLD_COORDS && !NEEDS_LIGHTMAP_COORDINATE && !NUM_MATERIAL_TEXCOORDS
		Interpolants.Dummy = float4(0,0,0,0);
	#endif

	return Interpolants;
}

/** for depth-only pass */
float4 VertexFactoryGetWorldPosition(FPositionOnlyVertexFactoryInput Input)
{
	return MulMatrix(LocalToWorld,Input.Position);
}

float4 VertexFactoryGetPreviousWorldPosition(FVertexFactoryInput Input)
	{
	return MulMatrix(PreviousLocalToWorld,Input.Position);
}

/**
* Transform a vector from world space to tangent space
*
* @param Input - vertex input stream structure
* @param TangentBasis - 3x3 matrix to transform to tangent space
* @param WorldVector - vector in world space to transform 
* @return vector in tangent space
*/
float3 VertexFactoryWorldToTangentSpace( FVertexFactoryInput Input, float3x3 TangentBasis, float3 WorldVector )
{
	float3x3 temp = LocalToWorld;
	// we use a straight mul here because we are generating the matrix, so we don't worry about column major vs row major (which is what MulMatrix manages per-platform)
	return mul(TangentBasis, MulMatrix(transpose(temp),WorldVector));
}

half3 VertexFactoryGetWorldNormal(FVertexFactoryInput Input)
{
	half4 Normal = TangentBias(Input.TangentZ);
	Normal.w = 0;

	half4 WorldNormal = MulMatrix( LocalToWorld, Normal );
	return WorldNormal.xyz;
}
